


terraform {
  required_providers {
    rancher2 = {
      source  = "rancher/rancher2"
      version = "4.1.0"
    }
  }
}

variable "server_url" {
  default = "https://{{rancher_hostname}}"
}

variable "admin_password" {
  default = "{{rancher_admin_password}}"
}

provider "rancher2" {
  alias = "bootstrap"

  api_url   = var.server_url
  bootstrap = true
  insecure  = true
}

provider "rancher2" {
  alias = "admin"

  api_url   = rancher2_bootstrap.admin.url
  token_key = rancher2_bootstrap.admin.token
  insecure  = true
}

resource "rancher2_bootstrap" "admin" {
  provider         = rancher2.bootstrap
  initial_password = var.admin_password
  password         = var.admin_password
  telemetry        = true
  token_ttl        = 60
  token_update     = true
}

resource "rancher2_setting" "server_url" {
  provider = rancher2.admin
  name     = "server-url"
  value    = var.server_url
}

resource "rancher2_cluster" "openg2p_k8s_cluster" {
  provider    = rancher2.admin
  name        = "openg2p-k8s-cluster"
  description = "OpenG2P K8S Cluster"
}



output "cluster_registration_token" {
  sensitive = true
  value     = rancher2_cluster.openg2p_k8s_cluster.cluster_registration_token[0].token
}


output "cluster_registration_command" {
  value     = rancher2_cluster.openg2p_k8s_cluster.cluster_registration_token[0].command
}
